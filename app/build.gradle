apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.lusifer.shabdkosh"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        config {
            keyAlias 'Shabdkosh'
            keyPassword 'password'
            storeFile file('../Shabdkosh.jks')
            storePassword 'password'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            zipAlignEnabled true
        }
        debug {
            versionNameSuffix " Debug"
            debuggable true
        }
        buildTypes.each {
            it.buildConfigField 'String', 'API_KEY', "\""+getApiKey()+"\""
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        debug {
            java.srcDirs = ['src/debug/java', 'src/main/java/']

            res.srcDirs = ['src/debug/res', 'src/main/res/']

        }
        release {
            java.srcDirs = ['src/release/java', 'src/main/java/']

            res.srcDirs = ['src/release/res', 'src/main/res/']
        }
    }
}

dependencies {

    final SUPPORT_LIBRARY_VERSION = '25.2.0'
    final RETROFIT_VERSION = '2.1.0'
    final OK_HTTP_VERSION = '3.6.0'
    final MOCKITO_VERSION = '2.6.2'
    final HAMCREST_VERSION = '1.3'
    final ESPRESSO_VERSION = '2.2.2'
    final RUNNER_VERSION = '0.4'
    final BUTTERKNIFE_VERSION = '8.5.1'
    final STETHO_VERSION = '1.4.2'
    final DBFLOW_VERSION = '4.0.0-beta5'

    def jUnit = "junit:junit:4.12"
    def mockito = "org.mockito:mockito-core:$MOCKITO_VERSION"

    // App Dependencies
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"


    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"


    compile "com.squareup.okhttp3:logging-interceptor:$OK_HTTP_VERSION"

    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    compile 'com.github.arimorty:floatingsearchview:2.0.3'
    compile 'com.github.silvestrpredko:dot-progress-bar:0.1.4@aar'

    debugCompile "com.facebook.stetho:stetho:$STETHO_VERSION"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$STETHO_VERSION"

    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:$DBFLOW_VERSION"

    compile "com.github.Raizlabs.DBFlow:dbflow-core:$DBFLOW_VERSION"
    compile "com.github.Raizlabs.DBFlow:dbflow:$DBFLOW_VERSION"

    compile 'com.google.android:flexbox:0.2.5'

    // Instrumentation test dependencies
    androidTestCompile jUnit
    androidTestCompile mockito
    androidTestCompile "org.mockito:mockito-android:$MOCKITO_VERSION"
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude module: 'support-annotations'

    }
    androidTestCompile "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestCompile "com.android.support.test:runner:$RUNNER_VERSION"
    androidTestCompile "com.android.support.test:rules:$RUNNER_VERSION"

    // Unit tests dependencies
    testCompile jUnit
    testCompile mockito
    testCompile "org.hamcrest:hamcrest-core:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-library:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-integration:$HAMCREST_VERSION"
    testCompile 'org.robolectric:robolectric:3.1'


}

def getApiKey(){
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['API_KEY']
}
